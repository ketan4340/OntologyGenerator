# ここに書かれたルールは全て適用される

# 文節からその自立語の概念を得るショートカット
IF (ex:catMean) {
  ?cls jass:consistsOfCategorem [jass:means ?Cpt] .
} THEN {
  ?cls jass:categoremMeans ?Cpt .
};

# 主語 (jass:subject)
IF (ex:subject) {
  ?stc a jass:Sentence ; 
       jass:consistsOfClauses/rdf:rest*/rdf:first ?cls .
  ?cls jass:consistsOfAdjuncts/rdf:rest*/rdf:first 
           [jass:mainPoS "助詞" ; jass:infinitive "は"] .
}THEN{
  ?stc jass:subject ?cls .
};

# 述語 (jass:predicate)
IF (ex:predicate) {
  ?stc a jass:Sentence ;
       jass:subject/jass:dependTo ?clsP .
}THEN{
  ?stc jass:predicate ?clsP .
};

# 目的語 (jass:object)
IF (ex:object) {
  ?stc a jass:Sentence ;
       jass:subject ?clsS ;
       jass:predicate ?clsP ;
       jass:consistsOfClauses/rdf:rest*/rdf:first ?clsO .
  ?clsO jass:dependTo ?clsP ; # 目的語は述語に係る
        jass:consistsOfAdjuncts/rdf:rest*/rdf:first
            [jass:mainPoS "助詞" ; jass:infinitive ?adjinf] . 
  FILTER(?clsO != ?clsS) . # 主語は目的語ではない
  VALUES (?inf ?case) {
    ("が" jass:caseGAclause) 
    ("を" jass:caseWOclause) 
    ("に" jass:caseNIclause) 
    ("へ" jass:caseHEclause) 
    ("から" jass:caseKARAclause) 
    ("より" jass:caseYORIclause) 
    ("で" jass:caseDEclause) 
  }
  FILTER(?inf = ?adjinf)
} THEN {
  ?stc jass:object ?clsO ; 
       ?case ?clsO .
};

#形容詞的名詞(緑色、辛味)などへのマーキング
IF (ex:attributiveNoun1) {
  ?wrd a jass:Categorem ; jass:subPoS1 ?pos .
  FILTER(?pos IN("形容動詞語幹", "ナイ形容詞語幹"))
} THEN {
  ?wrd jass:feature "attributive" .
};
IF (ex:attributiveNoun2) {
  ?wrd a jass:Categorem ; jass:infinitive ?infi .
  FILTER(regex(?infi,"(色|形|型|味|的)$"))
} THEN {
  ?wrd jass:feature "attributive" .
};