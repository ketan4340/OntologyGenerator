# ここに書かれたルールは全て適用される

# 主語 (jass:subject)
# 「には」「では」は除く
IF (ex:subject) {
  ?stc a jass:Sentence ; 
       jass:clauses/rdf:rest*/rdf:first ?cls .
  ?cls jass:adjuncts/rdf:rest*/rdf:first 
           [jass:mainPoS "助詞" ; jass:infinitive "は"].
  MINUS{?cls jass:adjuncts/rdf:rest*/rdf:first 
           [jass:mainPoS "助詞" ; jass:infinitive "で"]}
  MINUS{?cls jass:adjuncts/rdf:rest*/rdf:first 
           [jass:mainPoS "助詞" ; jass:infinitive "に"]}
}THEN{
  ?stc jass:subject ?cls .
};

# 述語 (jass:predicate)
IF (ex:predicate) {
  ?stc a jass:Sentence ;
       jass:subject/jass:dependTo ?clsP .
}THEN{
  ?stc jass:predicate ?clsP .
};

# 目的語 (jass:object)
IF (ex:object) {
  ?stc a jass:Sentence ;
       jass:subject ?clsS ;
       jass:predicate ?clsP ;
       jass:clauses/rdf:rest*/rdf:first ?clsO .
  ?clsO jass:dependTo ?clsP ; # 目的語は述語に係る
        jass:adjuncts/rdf:rest*/rdf:first
            [jass:subPoS1 "格助詞" ; jass:infinitive ?adjinf] . 
  FILTER(?clsO != ?clsS) . # 主語は目的語ではない
  VALUES (?inf ?case) {
    ("が" jass:caseGAclause) 
    ("を" jass:caseWOclause) 
    ("に" jass:caseNIclause) 
    ("へ" jass:caseHEclause) 
    ("から" jass:caseKARAclause) 
    ("より" jass:caseYORIclause) 
    ("で" jass:caseDEclause) 
  }
  FILTER(?inf = ?adjinf)
} THEN {
  ?stc jass:object ?clsO ; 
       ?case ?clsO .
};
