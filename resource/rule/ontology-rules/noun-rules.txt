# 数値と単位
IF (numeral value) {
  ?stc 	rdf:type jass:Sentence ;
  		jass:subject/jass:categoremMeans ?rsrcS ;
 		jass:predicate ?clsP .
  ?clsP jass:consistsOfCategorem ?prs .
#  ?prs jass:consistsOfDependent/rdf:rest/rdf:first/jass:name ?length .
#  FILTER regex(?length, "長$")
  ?prs jass:consistsOfHead/jass:consistsOfMorphemes/rdf:rest* (?valrsc ?unitrsc) .
  ?valrsc jass:name ?val .
  ?unitrsc jass:name ?unit .
  FILTER(regex(str(?val), "[0-9０-９]+?(\\.[0-9０-９]+?)??"))
} THEN {
  ?rsrcS sio:SIO_000216 _:bn .
#  _:bn rdf:type ?length .
  _:bn rdf:value ?val .
  _:bn sio:SIO_000221 ?unit .
};

# 総称 (owl:sameAs)
IF (equivalentClass) {
  ?stc rdf:type jass:Sentence ; 
       jass:subject/jass:categoremMeans ?rsrcS ; 
       jass:predicate ?SclsP .
  ?SclsP jass:consistsOfCategorem 	?prs .
  ?prs jass:consistsOfHead/jass:infinitive ?equiv .
  FILTER(?equiv IN("総称", "通称", "別称", "異称", "別名"))
  ?prs jass:consistsOfDependent/rdf:rest* ?node . # 形態素の最後尾
  ?node rdf:rest rdf:nil ;
	rdf:first/jass:categoremMeans ?rsrcp .
}THEN{
  ?rsrcS owl:equivalentClass ?rsrcp .
};

# サ変接続の動詞
IF (Action(サ変)) {
  ?stc a jass:Sentence ; 
       jass:subject/jass:categoremMeans ?rsrcS ; 
       jass:predicate/jass:consistsOfCategorem 
           [jass:subPoS1 "サ変接続" ; jass:means ?rsrcVb] .
  OPTIONAL{?stc jass:object/jass:categoremMeans ?rsrcO . }
} THEN {
  _:v a ?rsrcVb ; 
      schema:agent ?rsrcS ;
      schema:object ?rsrcO .
  ?rsrcVb rdfs:subClassOf schema:Action .
};

# 形容詞的名詞
IF (attributive) {
  ?stc a jass:Sentence ;
	jass:subject/jass:categoremMeans ?rsrcS ;
	jass:predicate ?clsP . 
  ?clsP jass:consistsOfCategorem ?wrdP .
  ?wrdP jass:feature "attributive" ;
	jass:means ?rsrcP . 
}THEN{
  ?rsrcS jass:attributeOf ?rsrcP .
};


# 概念階層 (rdfs:subClassOf)
IF (subClassOf) {
  ?stc a jass:Sentence ;
	jass:subject/jass:categoremMeans ?rsrcS ;
	jass:predicate ?clsP . 
  ?clsP jass:consistsOfCategorem ?wrdP .
  ?wrdP jass:mainPoS "名詞" ;
	jass:means ?rsrcP . 
  MINUS{?wrdP jass:subPoS1 "接尾".}
} THEN {
  ?rsrcS rdfs:subClassOf ?rsrcP .
};